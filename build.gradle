/*
 * Copyright 2018-2024 Pranav Pandey
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.versions = [
            'compileSdk'  : 35,
            'minSdk'      : 19,
            'targetSdk'   : 35,
            'buildTools'  : '35.0.0',
            'multidex'    : '2.0.1',
            'dynamic'     : '4.6.1',
            'fragment'    : '1.7.0',
            'flexbox'     : '3.0.0',
            'kotlin'      : '1.9.24',
            'locale'      : '2.5.0',
            'material'    : '1.12.0',
            'preferences' : '2.4.0',
            'swiperefresh': '1.1.0',
            'theme'       : '4.6.0',
            'toasts'      : '4.3.0',
            'work'        : '2.9.1'
    ]

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

ext {
    projectName = 'dynamic-support'
    projectDesc = 'A complete library to build apps for Android with a built-in theme engine.'
    versionDesc = 'A complete library to build apps for Android 4.0 (API 14) and above ' +
            'with a built-in theme engine.'
    referenceTitle = 'Dynamic Support API reference'

    siteUrl = 'https://github.com/pranavpandey/dynamic-support'
    gitUrl = 'https://github.com/pranavpandey/dynamic-support'
    issueUrl = 'https://github.com/pranavpandey/dynamic-support/issues'
    githubUrl = 'pranavpandey/dynamic-support'

    mavenRepo = 'android'
    mavenGroup = 'com.pranavpandey.android'
    mavenDir = 'com/pranavpandey/android'
    mavenArtifactId = 'dynamic-support'
    mavenInceptionYear = 2018
    mavenVersion = '6.4.0'
    mavenVersionCode = 37
    sampleVersionCode = 38

    developerId = 'pranavpandey'
    developerName = 'Pranav Pandey'
    developerEmail = 'dynamic@pranavpandey.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    licenseDistribution = 'repo'
    allLicenses = ["Apache-2.0"]

    publication = 'local.properties'

    ext["signing.keyId"] = ''
    ext["signing.password"] = ''
    ext["signing.secretKeyRingFile"] = ''

    ossrhUsername = ''
    ossrhPassword = ''
    sonatypeStagingProfileId = ''
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'

File publish = project.rootProject.file("${publication}")
if (publish.exists()) {
    Properties properties = new Properties()
    new FileInputStream(publish).withCloseable { is -> properties.load(is) }
    properties.each { name, value -> ext[name] = value }
}

nexusPublishing {
    repositories {
        sonatype {
            username = ossrhUsername
            password = ossrhPassword
            stagingProfileId = sonatypeStagingProfileId
        }
    }
}
